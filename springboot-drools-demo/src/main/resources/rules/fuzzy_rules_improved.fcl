FUNCTION_BLOCK gear_selector
VAR_INPUT
    speed : REAL;   // Input variable for speed (range: 0 to 120)
    rpm : REAL;   // Input variable for rpm (range: 0 to 7000)
END_VAR

VAR_OUTPUT
    gear : REAL;    // Output variable for selected gear
END_VAR


FUZZIFY speed
    // gear1 limits 0-35
    TERM gear1_low := (0, 1) (10, 0);        // Low speed for gear 1
    TERM gear1_medium := (0, 0) (5, 1) (16, 1) (25, 0); // Medium speed for gear 1
    TERM gear1_high := (20, 0) (22, 1) (35, 0); // High speed for gear 1

    //gear2 limits 7-60
    TERM gear2_low := (7, 0) (15, 1) (19, 0); // Low speed for gear 2
    TERM gear2_medium := (17, 0) (20, 1) (30, 1) (45, 0); // Medium speed for gear 2
    TERM gear2_high := (35, 0) (40, 1) (60, 0); // High speed for gear 2

    //gear3 limits 18-80
    TERM gear3_low := (18, 0) (23.5, 1) (30, 0); // Low speed for gear 3
    TERM gear3_medium := (28, 0) (35, 1) (45, 1) (60, 0); // Medium speed for gear 3
    TERM gear3_high := (50, 0) (55, 1) (80, 0); // High speed for gear 3

    //gear4 limits 25-100
    TERM gear4_low := (20, 0) (30, 1) (50, 0); // Low speed for gear 4
    TERM gear4_medium := (35, 0) (45, 1) (55, 1) (80, 0); // Medium speed for gear 4
    TERM gear4_high := (65, 0) (70, 1) (80, 1) (100, 0.3) (120, 0); // High speed for gear 4

    //gear5 limits 40-115
    TERM gear5_low := (40, 0) (50, 1) (60, 0); // Low speed for gear 5
    TERM gear5_medium := (55, 0) (60, 1) (78, 1) (110, 0); // Medium speed for gear 5
    TERM gear5_high := (90, 0) (100, 1) (130, 1); // High speed for gear 5
END_FUZZIFY

FUZZIFY rpm
    // gear1 limits 0-35
    TERM gear1_rpm := (0, 0) (1000, 1) (2200, 1) (2900, 0.3) (3400, 0.1) (6000, 0); // rpm for gear 1

    //gear2 limits 9-60
    TERM gear2_rpm := (0, 0) (1500, 1) (2500, 1) (3300, 0.3) (3800, 0.1) (6000, 0); // rpm for gear 2

    //gear3 limits 18-80
    TERM gear3_rpm := (0, 0) (1850, 1) (2600, 1) (2900, 0.3) (3100, 0.1) (5000, 0); // rpm for gear 3

    //gear4 limits 25-100
    TERM gear4_rpm := (0, 0) (1900, 1) (2050, 1) (2200, 0.6) (2400, 0.3) (4000, 0); // rpm for gear 4

    //gear5 limits 40-115
    TERM gear5_rpm := (0, 0) (1700, 0.3)  (2300, 1) (3800, 0); // rpm for gear 5
END_FUZZIFY

DEFUZZIFY gear
    TERM gear1 := (0, 1) (17.5, 1) (35, 0); // gear1 limits 0-35
    TERM gear2 := (9, 0) (34.5, 1) (60, 0); //gear2 limits 9-60
    TERM gear3 := (18, 0) (49, 1) (85, 0); //gear3 limits 18-80
    TERM gear4 := (25, 0) (62.5, 1) (100, 0); //gear4 limits 25-100
    TERM gear5 := (40, 0) (80, 1) (130, 1); //gear5 limits 40-115
    METHOD : COG;
    DEFAULT := 0;
END_DEFUZZIFY

RULEBLOCK GearSelection
    AND : MIN;
    ACT : MIN;
    ACCU : MAX;

    // Gear 1 rules
    RULE 1 : IF (speed IS gear1_low OR speed IS gear1_medium) AND rpm IS gear1_rpm THEN gear IS gear1;

    // Gear 2 rules
    RULE 2 : IF (speed IS gear1_high AND speed IS gear2_low) AND rpm IS gear2_rpm THEN gear IS gear2;
    RULE 3 : IF speed IS gear2_medium AND rpm IS gear2_rpm THEN gear IS gear2;

    // Gear 3 rules
    RULE 4 : IF speed IS gear2_high AND speed IS gear3_low AND rpm IS gear3_rpm THEN gear IS gear3;
    RULE 5 : IF speed IS gear3_medium AND rpm IS gear3_rpm THEN gear IS gear3;

    // Gear 4 rules
    RULE 6 : IF speed IS gear3_high AND speed IS gear4_low AND rpm IS gear4_rpm THEN gear IS gear4;
    RULE 7 : IF speed IS gear4_medium AND rpm IS gear4_rpm THEN gear IS gear4;
    RULE 8 : IF speed IS gear4_high AND speed IS gear5_low AND rpm IS gear5_rpm THEN gear IS gear4;

    // Gear 5 rules
    RULE 9 : IF (speed IS gear5_medium OR speed IS gear5_high) AND rpm IS gear5_rpm THEN gear IS gear5;

END_RULEBLOCK

END_FUNCTION_BLOCK
